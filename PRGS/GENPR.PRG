PROCEDURE GENPR
	LPARAMETERS lnMonto,lnIva,lnRetIva,lcIdPresup
	
	MESSAGEBOX("genpr")
	SET SQLBUFFERING ON 
	DO EQUIPOS WITH lcIdPresup
	DO MATERIALES WITH lcIdPresup
	DO TRABAJOS WITH lcIdPresup
	SELECT ;
		MAESTRO.IDCLIENTE									AS IDCLIENTE,;
		CONTENIDO("CLIENTE",lcIdPresup)						AS CLIENTE,;
		NVL(CLIENTES.NOMBRE," ")							AS NOMBRE,;
		MAESTRO.IDPRESUP									AS IDPRESUP,;
		MAESTRO.ATN											AS ATN,;
		MAESTRO.FECHA										AS FECHA,;
		CAST(CONTENIDO("CABECERA",lcIdPresup) AS MEMO)		AS CABECERA,;
		CAST(CONTENIDO("PIECABECERA",lcIdPresup) AS MEMO)	AS PIECABECERA,;
		CAST(CONVNUM(lnMonto+lnIva-lnRetIva) AS CHAR(80))	AS MONTOLETRA,;
		lnMonto												AS SUBTOTAL,;
		lnIva												AS IVA ,;
		lnRetIva											AS RETIVA,;
		lnMonto+lnIva-lnRetIva								AS TOTAL,;
		CAST(VALIDEZ() AS CHAR(50)) 						AS VALIDEZ,;
		CAST(CONDICIONES(MAESTRO.FPAGO)AS CHAR(75))			AS CONDICIONES,;
		CAST(TENTREGA(MAESTRO.TENTREGA) AS CHAR(30))		AS TENTREGA,;
		CAST(TINICIO(MAESTRO.TINICIO) AS CHAR(30))			AS TINICIO,;
		CAST(CONTENIDO("NOTAS",lcIdPresup) as MEMO)			AS NOTAS,;		
		MAESTRO.COMEN1										AS OBSERVACIONES,;
		MAESTRO.COMEN2										as OBSERVACIONES2,;
		CAST(CONTENIDO("FIRMA",lcIdPresup) AS MEMO)			AS FIRMAS,;								
		MAESTRO.INCLUIRNOTAS								AS INCLUIRNOTAS,;
		MAESTRO.DEPTO										AS DEPTO,;
		ALLTRIM(MAESTRO.PROYECTO+" "+NVL(PROYECTOS.NOMBRE,""))			AS PROYECTO,;
		MAESTRO.IDMECA										AS IDMECA,;
		MAESTRO.MECADESCRIP									AS MECADESCRIP;
	FROM; 
		MAESTRO LEFT JOIN CLIENTES ON MAESTRO.IDCLIENTE=CLIENTES.IDCLIENTE;
				LEFT JOIN proyectos ON maestro.proyecto=proyectos.idproyectos;
	WHERE ;
		MAESTRO.IDPRESUP=lcIdPresup ;
	INTO CURSOR;
		PR READWRITE 
	INDEX ON IDPRESUP TAG IDPRESUP 
	SET ORDER TO IDPRESUP 
	
	SET RELATION TO IDPRESUP INTO PREQ ,IDPRESUP INTO PRMAT, IDPRESUP INTO PRMO 
	DO porcglobal WITH lcIdPresup
	DO cerrartbs
ENDPROC 


PROCEDURE cerrartbs

	IF USED("Contenido")	
		SELECT contenido
		USE 
	ENDIF 
	IF USED("Firma")	
		SELECT firma
		USE 
	ENDIF 
	IF USED("Notas")	
		SELECT notas
		USE 
	ENDIF 
	IF USED("Piecabecera")	
		SELECT piecabecera
		USE 
	ENDIF 
	IF USED("Cabecera")	
		SELECT cabecera
		USE 
	ENDIF 
	IF USED("Cliente")	
		SELECT cliente
		USE 
	ENDIF 


	IF USED("Personnaturales")	
		SELECT personnaturales
		USE 
	ENDIF 
	IF USED("Clasificacioncontribuyentes")	
		SELECT clasificacioncontribuyentes
		USE 
	ENDIF 
	IF USED("Contribuyentes")	
		SELECT contribuyentes
		USE 
	ENDIF 
	IF USED("Proveedores")	
		SELECT proveedores
		USE 
	ENDIF 
	IF USED("Presupuestos")	
		SELECT Presupuestos
		USE 
	ENDIF 
	
ENDPROC 
PROCEDURE VALIDEZ
	RETURN "Validez de la oferta:  30 dias "
ENDPROC 

PROCEDURE CONDICIONES
	LPARAMETERS lcFpago
	LOCAL lcReturn 
	lcReturn="Condiciones de pago:"
	IF !USED("formapagopresup")
		USE formapagopresup IN 0
	ENDIF 

	SELECT FORMAPAGOPRESUP
	SET ORDER TO IDFPAGO
	SEEK LCFPAGO
		IF FOUND()
			lcReturn=lcReturn+FORMAPAGOPRESUP.DESCRIPCION
		ENDIF 	
	RETURN lcReturn
ENDPROC 

PROCEDURE TENTREGA
	LPARAMETERS lnTentrega
	LOCAL lcReturn
	lcReturn="Tiempo de entrega: " +TRANSFORM(lnTentrega)+" dia"+IIF(lnTentrega>1,"s","")
		RETURN lcReturn
ENDPROC 

PROCEDURE TINICIO
	LPARAMETERS lnInicio
	LOCAL lcReturn
	lcReturn="Tiempo de inicio: " +TRANSFORM(lnInicio)+" dia"+IIF(lnInicio>1,"s","")
		RETURN lcReturn
ENDPROC 


PROCEDURE CONTENIDO
	LPARAMETERS lcParte,lcIdpresup
	IF !USED("presup_param")
		USE presup_param IN 0
	ENDIF 
	SELECT PRESUP_PARAM
	SET ORDER TO IDPRESUP
	SEEK lcIdpresup
	IF !FOUND()
		INSERT INTO presup_param			;
					(idpresup,&lcParte)		;
				SELECT 						;
					lcIdpresup,				;
					&lcParte 				;
				FROM						;
					PRESUP_PARAM 			;
				WHERE 						;
					PRESUP_PARAM.pcpal=.t.	
	ENDIF 
	
	SELECT lcIdpresup,&lcParte FROM	PRESUP_PARAM WHERE PRESUP_PARAM.pcpal=.t. INTO CURSOR &lcParte 
	SELECT IDPRESUP,&lcParte FROM PRESUP_PARAM WHERE IDPRESUP=lcIdpresup INTO CURSOR contenido
	IF EMPTY(contenido.&lcParte)
		SELECT &lcParte 
		IF !LCPARTE="CLIENTE"
			COPY MEMO &lcParte TO MEMOTXT.TXT
		ELSE
			lcCLiente=&lcParte
		ENDIF 
		
		IF !USED("PRESUP_PARAM")
			USE PRESUP_PARAM
		ENDIF 
		
		SELECT PRESUP_PARAM
		SEEK lcIdpresup
			IF FOUND()
				IF !LCPARTE="CLIENTE"
					APPEND MEMO &lcParte FROM MEMOTXT.TXT
					DELETE FILE MEMOTXT.TXT
				ELSE
					REPLACE &lcParte WITH lcCLiente
				ENDIF 
			ENDIF 
	ENDIF 
	SELECT PRESUP_PARAM
	SEEK lcIdpresup
	IF FOUND()
		RETURN PRESUP_PARAM.&lcParte
	ENDIF 	
ENDPROC 


PROCEDURE MATERIALES 
	LPARAMETERS lcIdPresup
	SELECT ;
		lcIdPresup AS IDPRESUP,;
		REFERENCIA AS REFERENCIA,;
		DESCRIPCION	AS DESCRIPCION,;
		CAST(ROUND(CANTIDAD,2) AS N(4,2))	AS CANTIDAD,;
		CAST(DETALLEPRESUPUESTO.UNIDAD AS C(20)) AS UNIDAD,;
		CAST(ROUND(COSTO,2) AS N(10,2)) AS COSTO,;
		CAST(ROUND(PRECIO,2) AS N(10,2)) AS PRECIO;		
	FROM;
		DETALLEPRESUPUESTO ;
	WHERE ;
		DETALLEPRESUPUESTO.IDPRESUP=lcIdpresup AND ;
		DETALLEPRESUPUESTO.tp=1;
	INTO CURSOR ;
		PRMAT READWRITE 
	INDEX ON IDPRESUP TAG IDPRESUP 
	SET ORDER TO IDPRESUP
ENDPROC 

PROCEDURE TRABAJOS
	LPARAMETERS lcIdPresup
	SELECT ;
		lcIdPresup AS IDPRESUP,;
		REFERENCIA AS REFERENCIA,;
		DESCRIPCION	AS DESCRIPCION,;
		ROUND(CANTIDAD,2)	AS CANTIDAD,;
		ROUND(COSTO,2) AS COSTO,;
		ROUND(PRECIO,2) AS PRECIO;		
	FROM;
		DETALLEPRESUPUESTO;
	WHERE ;
		DETALLEPRESUPUESTO.IDPRESUP=lcIdpresup AND ;
		DETALLEPRESUPUESTO.tp=2;
	INTO CURSOR ;
		PRMO READWRITE 
	INDEX ON IDPRESUP TAG IDPRESUP
	SET ORDER TO IDPRESUP 
ENDPROC 

PROCEDURE EQUIPOS 
	LPARAMETERS lcIdPresup
	IF !SUBSTR(lcIdPresup,1,2)="PA"
		SELECT ;
			lcIdPresup 				AS IDPRESUP,;
			EQUIPOSPRESUP.IDEQUIPO	AS IDEQUIPO,;
			MODELO					AS MODELO,;
			SERIE					AS SERIE,;
			MARCA					AS MARCA,;
			CAPACIDAD				AS CAPACIDAD,;
			UBICACION				AS UBICACION;	
		FROM ;
			EQUIPOSPRESUP,EQUIPOS;
		WHERE ;
			EQUIPOSPRESUP.IDEQUIPO=EQUIPOS.IDEQUIPO AND ;
			EQUIPOSPRESUP.IDPRESUP=lcIdPresup;
		INTO CURSOR ;
			PREQ READWRITE 
	ELSE 
		SELECT ;
			lcIdPresup 					AS IDPRESUP,;
			VEHICULOSPRESUP.IDVEHICULO	AS IDEQUIPO,;
			PLACA						AS MODELO,;
			MARCA						AS SERIE,;
			AÑO							AS MARCA,;
			MODELO						AS CAPACIDAD,;
			KILOMETRAJE					AS UBICACION;
		FROM ;
			VEHICULOSPRESUP,VEHICULOS;
		WHERE ;
			VEHICULOSPRESUP.IDVEHICULO=VEHICULOS.IDVEHICULO AND;
			VEHICULOSPRESUP.IDPRESUP=lcIdPresup ;
		INTO CURSOR;
			PREQ READWRITE 
			
			**YEC
			SELECT ;
			lcIdPresup 					AS IDPRESUP,;
			VEHICULOSPRESUP.IDVEHICULO	AS IDEQUIPO,;
			PLACA						AS MODELO,;
			MARCA						AS SERIE,;
			AÑO							AS MARCA,;
			MODELO						AS CAPACIDAD,;
			KILOMETRAJE					AS UBICACION;
		FROM ;
			VEHICULOSPRESUP,VEHICULOS;
		WHERE ;
			VEHICULOSPRESUP.IDVEHICULO=VEHICULOS.IDVEHICULO AND;
			VEHICULOSPRESUP.IDPRESUP=lcIdPresup
	ENDIF 		
	INDEX ON IDPRESUP TAG IDPRESUP 
	SET ORDER TO IDPRESUP 
		
ENDPROC 
PROCEDURE PORCGLOBAL
LPARAMETERS lcIdPresup
	IF VARTYPE(gnPorcPresup)="U"
		PUBLIC gnPorcPresup
	ENDIF 
	SELECT ;
		SUM(CANTIDAD*PRECIO)/SUM(CANTIDAD*COSTO) AS PORC;
	FROM;
		DETALLEPRESUPUESTO;
	WHERE ;
		DETALLEPRESUPUESTO.IDPRESUP=lcIdpresup;
	INTO ARRAY  ;
		gnPorc
		
	IF VARTYPE(gnPorcPresup)="X"
		gnPorcPresup=0
	ELSE
		gnPorcPresup=gnPorc
	ENDIF 
ENDPROC 